package "flatlang/git"

import "flatlang/system/ExecutionResponse"

class {
  public construct(
    visible String workingDirectory = System.workingDirectory
  ) {}

  public async clone(
    String url,
    Int: depth = 0,
    String: outputDirectory = null,
    String: branch = null,
    Bool: silent = false
  ) => Git.clone(
    url,
    depth,
    outputDirectory,
    branch,
    workingDirectory,
    silent
  )

  public static async clone(
    String url,
    Int: depth = 0,
    String: outputDirectory = null,
    String: branch = null,
    String: workingDirectory = System.workingDirectory,
    Bool: silent = false
  ) -> ExecutionResponse {
    let args = [
      "git",
      "clone",
      url
    ]

    if (depth > 0) {
      args.addAll(["--depth", "#depth"])
    }

    if (branch) {
      args.addAll(["--branch", branch])
    }

    if (outputDirectory) {
      args.add(outputDirectory)
    }

    return System.execute(args, workingDirectory, silent)
  }

  public async checkout(
    String ref,
    Bool: createBranch = false,
    Bool: silent = false
  ) => Git.checkout(
    ref,
    workingDirectory,
    createBranch,
    silent
  )

  public static async checkout(
    String ref,
    String: workingDirectory = System.workingDirectory,
    Bool: createBranch = false,
    Bool: silent = false
  ) -> ExecutionResponse {
    let args = [
      "git",
      "checkout"
    ]

    if (createBranch) {
      args.add("-b")
    }

    args.add(ref)

    return System.execute(args, workingDirectory, silent)
  }

  public class PullResponse {
    construct(
      visible Bool: upToDate,
      visible Bool: success,
      visible ExecutionResponse: executionResponse
    ) {}
  }

  public async pull(
    Bool: silent = false
  ) => Git.pull(
    workingDirectory,
    silent
  )

  public static async pull(
    String: workingDirectory = System.workingDirectory,
    Bool: silent = false
  ) -> PullResponse {
    let args = [
      "git",
      "pull"
    ]

    let executionResponse = System.execute(args, workingDirectory, silent)
    let success = executionResponse.exitCode == 0
    let upToDate = success && executionResponse.stdout.first?.trim() == "Already up to date."

    return new PullResponse(
      upToDate,
      success,
      executionResponse
    )
  }

  public class GetConfigValueResponse {
    construct(
      visible String: value,
      visible Bool: success,
      visible ExecutionResponse: executionResponse
    ) {}
  }

  public async getConfigValue(
    String configValue,
    Bool: silent = false
  ) => Git.getConfigValue(
    configValue,
    workingDirectory,
    silent
  )

  public static async getConfigValue(
    String configValue,
    String: workingDirectory = System.workingDirectory,
    Bool: silent = false
  ) -> GetConfigValueResponse => null {
    let response = System.execute(
      ["git", "config", "--get", configValue],
      workingDirectory,
      silent: silent
    )

    if (response.exitCode == 0) {
      return new GetConfigValueResponse(
        value: response.stdout.first?.trim(),
        success: response.exitCode == 0,
        executionResponse: response
      )
    } else {
      throw new GitCommandException("git config failed", response)
    }
  }

  public class GetOriginUrlResponse extends GetConfigValueResponse {
    construct(GetConfigValueResponse response) {
      this(
        value: response.value,
        success: response.success,
        executionResponse: response.executionResponse
      )
    }

    construct(
      String: value,
      Bool: success,
      ExecutionResponse: executionResponse
    ) {
      super(value, success, executionResponse)
    }
  }

  public async getOriginUrl(
    Bool: silent = false
  ) => Git.getOriginUrl(
    workingDirectory,
    silent
  )

  public static async getOriginUrl(
    String workingDirectory = System.workingDirectory,
    Bool: silent = false
  ) =>
    new GetOriginUrlResponse(
      getConfigValue(
        "remote.origin.url",
        workingDirectory,
        silent
      )
    )
}